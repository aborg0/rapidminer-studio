buildscript {
	repositories {
		jcenter()
		maven { url 'https://maven.rapidminer.com/content/groups/public/' }
		//necessary for a better jgraphx
//		mavenLocal()
	}
	dependencies {
		classpath 'com.rapidminer.gradle:java-basics:0.3.3'
		classpath 'com.rapidminer.gradle:java-publishing:0.2.0'
		classpath 'com.rapidminer.gradle:java-signing:0.1.0'
        classpath "com.athaydes.gradle.osgi:osgi-run-core:1.3.1"
        //Check for the latest version here: http://plugins.gradle.org/plugin/com.jfrog.artifactory
//        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:+"
	}
}

//ext {
//	poiVersion = "3.10.1"
//}

apply plugin: 'com.rapidminer.java-basics'
//apply plugin: 'com.rapidminer.java-signing'
//apply plugin: 'com.rapidminer.java-publishing.agpl-v3'

//apply plugin: "com.jfrog.artifactory"

//artifactory {
//    contextUrl = "${artifactory_contextUrl}"   //The base Artifactory URL if not overridden by the publisher/resolver
//    publish {
//        repository {
//            repoKey = 'oss-release-local'
//            maven = true
//        }
//    }
//    resolve {
//        repository {
//            repoKey = 'libs-release'
//            maven = true
//        }
//    }
//}

apply plugin: 'osgi'
apply plugin: 'com.athaydes.osgi-run'

configurations {
    embedded
    compile.extendsFrom embedded
}

repositories {
	jcenter()
	maven { url 'https://maven.rapidminer.com/content/groups/public/' }
	mavenLocal()
}

runOsgi {
	configSettings = 'equinox'
	programArgs = '-console'
	bundles += project
	wrapInstructions {
	    // use regex to match file name of dependency
	    manifest( "activation*.*" ) {
	        instruction 'Bundle-SymbolicName', 'javax.activation'
	    }
	    manifest( "xpp3_min*.*" ) {
	        instruction 'Bundle-Version', '1.1.4.c'
	    }
		manifest("itext*.*") {
			instruction 'Import-Package', '!com.apple.mrj', '!com.lowagie.toolbox', '!org.bouncycastle.*', '*'
		}
		manifest("microba*.*") {
			instruction 'Import-Package', '!org.jgraph.*', '*'
		}
		manifest("rapidminer-studio-osx-adapter*.*") {
			instruction 'Import-Package', '!com.apple.*', '*'
		}
		manifest( /commons-logging.*/ ) {
            instruction 'Import-Package', '!javax.servlet,!org.apache.*,*'
	        instruction 'Bundle-SymbolicName', 'org.apache.commons.logging'
        }
		manifest( /avalon.*/ ) {
            instruction 'Import-Package', '!org.apache.*,*'
        }
        manifest( "rsyntaxtextarea*.*") {
        	instruction 'Import-Package', '!org.fife.ui.rsyntaxtextarea,*'
        	//Unfortunately this generates wrong bundle name, but it works.
        	instruction 'Bundle-SymbolicName', 'org.fife.rsyntaxtextarea'
        }
        manifest( "looks*.*") {
        	instruction 'Import-Package', '!com.sun.*,*'
        }
        manifest( "log4j*.*") {
        	instruction 'Import-Package', '!com.sun.*,*'
        }
        manifest( "logkit*.*") {
        }
        manifest("jgraphx*.*") {
        	instruction 'Export-Package', '*'
        }
	}
}

dependencies {

	// OS X adapter to add platform specific UI
	embedded 'com.rapidminer.studio:rapidminer-studio-osx-adapter:1.0.1'

	// RapidMiner license framework for license management
//	embedded 'com.rapidminer.license:rapidminer-license-api:3.1.0'
//	embedded 'com.rapidminer.license:rapidminer-license-commons:3.1.0'

	// RapidMiner API
	//compile 'com.mind_era.com.rapidminer:rapidminer-api:0.1.1'
	embedded 'com.rapidminer:rapidminer-api:0.1.1'

	// VLDocking as docking framework (https://code.google.com/p/vldocking/)
	embedded 'com.rapidminer.external:vldocking:1.0.0'

	// Freehep for vector graphic export (http://java.freehep.org/)
	compile('org.freehep:freehep-graphicsio-ps:2.3') {
		exclude group:'junit', module: 'junit'
		exclude group: 'org.freehep',module: 'freehep-graphicsio-tests'
	}
	compile('org.freehep:freehep-graphicsio-svg:2.3') {
		exclude group:'junit', module: 'junit'
		exclude group: 'org.freehep', module: 'freehep-graphicsio-tests'
	}

	// iText for PDF export (http://www.lowagie.com/iText/)
	compile('com.lowagie:itext:2.1.7'){
		exclude group: 'bouncycastle', module: 'bcmail-jdk14'
		exclude group: 'bouncycastle', module: 'bcprov-jdk14'
		exclude group: 'bouncycastle', module: 'bctsp-jdk14'
	}

	// RSyntaxTextArea adds text fields with syntax highlighting (http://fifesoft.com/rsyntaxtextarea/)
	compile 'com.fifesoft:rsyntaxtextarea:2.5.7'
	compile 'com.fifesoft:autocomplete:2.5.7'

	// JXL for the ability to read, write, and modify old format Microsoft Excel spreadsheets (http://www.jexcelapi.org)
	compile('net.sourceforge.jexcelapi:jxl:2.6.12') { exclude group: 'log4j', module: 'log4j' }

	// Apache POI for  manipulating various file formats based upon Office Open XML standards (http://poi.apache.org/)
	//unfortunately xmlbeans is corrupt (https://issues.apache.org/jira/browse/XMLBEANS-499), so we cannot use the original POI versions as they depend on it.
//	compile 'org.apache.poi:poi-ooxml:'+poiVersion
//	compile 'org.apache.poi:poi-scratchpad:'+poiVersion
	compile 'org.apache.servicemix.bundles:org.apache.servicemix.bundles.poi:3.11_1'
//	compile 'org.apache.servicemix.bundles:org.apache.servicemix.bundles.poi:3.13_1'
	osgiRuntime('org.apache.santuario:xmlsec:2.0.5') {
        transitive = false // transitive dependencies not included in OSGi runtime
    }
	osgiRuntime('org.apache.servicemix.bundles:org.apache.servicemix.bundles.xmlresolver:1.2_5')

	// JGoodies Looks for TODO (http://www.jgoodies.com/freeware/libraries/looks/)
	compile 'com.jgoodies:looks:2.2.2'

	// JUNG for displaying graphs and trees (http://jung.sourceforge.net/)
	compile 'net.sf.jung:jung-visualization:2.0.1'
	compile 'net.sf.jung:jung-graph-impl:2.0.1'

	// JFreeChart for chart rendering (http://www.jfree.org/jfreechart/)
	compile 'org.jfree:jfreechart:1.0.17'

	// Java Mail API for mail sending
	compile 'javax.mail:mail:1.4.7'

	// Groovy for 'Execute Script' operator (http://groovy.codehaus.org/)
	compile 'org.codehaus.groovy:groovy-all:2.3.3'

	// SwingX for various Swing components (https://swingx.java.net/)
	compile 'org.swinglabs.swingx:swingx-all:1.6.5'

	// XStreams for generic XML serialization (http://xstream.codehaus.org/)
//	compile 'com.thoughtworks.xstream:xstream:1.4.7'
	compile ('org.apache.servicemix.bundles:org.apache.servicemix.bundles.xstream:1.4.8_1') {
//		exclude group: 'xpp3', module: 'xpp3_min'
	}
	//osgiRuntime 'org.apache.servicemix.bundles:org.apache.servicemix.bundles.xpp3:1.1.4c_7'

	// XMLRPC for XMLRPC connections to Bugzilla (http://ws.apache.org/xmlrpc/)
	//compile('org.apache.xmlrpc:xmlrpc-client:3.1.3') { exclude group: 'junit', module: 'junit' }

	// HttpClient used by the Bugzilla XML RPC client (http://hc.apache.org/httpcomponents-client)
	//compile 'org.apache.httpcomponents:httpclient:4.5.1'

	// JAMA for matrix calculations (http://math.nist.gov/javanumerics/jama/)
	compile 'gov.nist.math:jama:1.0.3'

	// commons-math for matrix calculations (http://commons.apache.org/proper/commons-math/)
	compile 'org.apache.commons:commons-math3:3.4.1'

	// commons-lang for different String utility functions (http://commons.apache.org/proper/commons-lang/)
	compile 'commons-lang:commons-lang:2.6'

	// bouncycastle for encryption algorithms (https://www.bouncycastle.org/)
	//compile 'org.bouncycastle:bcprov-jdk15on:1.50'

	// jasypt for simplified encryption (http://www.jasypt.org/)
	//compile 'org.jasypt:jasypt:1.9.1:lite'

	// antlr for parsing expressions (http://www.antlr.org/)
	compile 'org.antlr:antlr4-runtime:4.5'
 
	// SLF4J API (http://www.slf4j.org) 
	compile 'org.slf4j:slf4j-api:1.7.12'
	osgiRuntime 'org.slf4j:slf4j-log4j12:1.7.13'
	
	// add testing suite
	//TODO should be test compile but RapidMiner src/main contains code that references JUnit
	compile 'junit:junit:4.12'

	// JGraphx for automatic operator arrangement (https://github.com/jgraph/jgraphx)
	embedded 'com.rapidminer.external:jgraphx:2.1.0.2'
	//Necessary because the maven jars available are OSGi bundles with nothing exported :(
	//compile 'com.mxgraph:jgraphx:3.4.0.3'
	//compile 'org.tinyjee.jgraphx:jgraphx:2.3.0.5'

	// JMathPlot for 2D and 3D plots like Box plot, Stick plot, etc. (https://code.google.com/p/jmathplot/)
	compile 'com.rapidminer.external:jmathplot:1.0.0'
	
	// Microba adds a Swing date picker (http://microba.sf.net/)
	embedded 'com.github.tdbear:microba:0.4.4.3'
}

jar {
	manifest {
	//com.sun.management should be provided.
    	instruction 'Import-Package', '!com.sun.*,!sun.*,!com.mxgraph.*,!com.michaelbaranov.microba.*,!com.vlsolutions.swing.*,*'
	}
	//From: http://kennethjorgensen.com/blog/2014/fat-jars-with-excluded-dependencies-in-gradle
	dependsOn configurations.runtime
	from {
        (configurations.embedded).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
}

task wrapper(type: Wrapper) { gradleVersion = '2.6' }

apply from: 'gradle/wsimport.gradle'
apply from: 'gradle/props.gradle'