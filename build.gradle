buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://maven.rapidminer.com/content/groups/public/' }
    }
    dependencies {
        classpath 'com.rapidminer.gradle:java-basics:0.4.0'
        classpath 'com.rapidminer.gradle:java-publishing:0.2.1'
        classpath "com.athaydes.gradle.osgi:osgi-run-core:1.6.0"
    }
}

apply plugin: 'com.rapidminer.java-basics'
apply plugin: 'com.rapidminer.java-publishing.agpl-v3'

apply plugin: 'osgi'
apply plugin: 'com.athaydes.osgi-run'

configurations {
    embedded
    compile.extendsFrom embedded
}

repositories {
    jcenter()
    maven { url 'https://maven.rapidminer.com/content/groups/public/' }
}

runOsgi {
	configSettings = 'equinox'
	osgiMain = 'org.eclipse.platform:org.eclipse.osgi:3.11.3'
	programArgs = '-console'
	bundles += project
	wrapInstructions {
	    // use regex to match file name of dependency
	    manifest( "activation*.*" ) {
	        instruction 'Bundle-SymbolicName', 'javax.activation'
	    }
	    manifest( "xpp3_min*.*" ) {
	        instruction 'Bundle-Version', '1.1.4.c'
	    }
		manifest("itext*.*") {
			instruction 'Import-Package', '!com.apple.mrj', '!com.lowagie.toolbox', '!org.bouncycastle.*', '*'
		}
		manifest("microba*.*") {
			instruction 'Import-Package', '!org.jgraph.*', '*'
		}
		manifest("rapidminer-studio-osx-adapter*.*") {
			instruction 'Import-Package', '!com.apple.*', '*'
		}
		manifest( /commons-logging.*/ ) {
            instruction 'Import-Package', '!javax.servlet,!org.apache.*,*'
	        instruction 'Bundle-SymbolicName', 'org.apache.commons.logging'
        }
		manifest( /avalon.*/ ) {
            instruction 'Import-Package', '!org.apache.*,*'
        }
        manifest( "rsyntaxtextarea*.*") {
        	instruction 'Import-Package', '!org.fife.ui.rsyntaxtextarea,*'
        	//Unfortunately this generates wrong bundle name, but it works.
        	instruction 'Bundle-SymbolicName', 'org.fife.rsyntaxtextarea'
        }
        manifest( "looks*.*") {
        	instruction 'Import-Package', '!com.sun.*,*'
        }
        manifest( "log4j*.*") {
        	instruction 'Import-Package', '!com.sun.*,*'
        }
//        manifest( "logkit*.*") {
//        }
        manifest("jgraphx*.*") {
        	instruction 'Export-Package', '*'
        }
	}
}

compileJava {
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
}

dependencies {

	// belt project for new data core
	compile 'com.rapidminer:belt:0.2.0'

	// belt adapter for conversion between old and new core
	compile ('com.rapidminer:belt-adapter:0.1.0'){
		exclude group: 'com.rapidminer.studio', module: 'rapidminer-studio-core'
	}

    // OS X adapter to add platform specific UI
	embedded 'com.rapidminer.studio:rapidminer-studio-osx-adapter:1.0.2'

	// RapidMiner license framework for license management
	embedded 'com.rapidminer.license:rapidminer-license-api:4.0.5'
	embedded('com.rapidminer.license:rapidminer-license-commons:4.0.5'){
		exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
		exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
	}

	// RapidMiner API
	embedded 'com.rapidminer:rapidminer-api:0.2.1'
	
	// Alphanumeric sorting
	compile 'com.rapidminer.external:alphanumeric-sorting:1.0.1'

	// VLDocking as docking framework (https://code.google.com/p/vldocking/)
	embedded 'com.rapidminer.external:vldocking:1.2.2'

	// Freehep for vector graphic export (http://java.freehep.org/)
	compile('org.freehep:freehep-graphicsio-ps:2.3') {
		exclude group:'junit', module: 'junit'
		exclude group: 'org.freehep',module: 'freehep-graphicsio-tests'
	}
	compile('org.freehep:freehep-graphicsio-svg:2.3') {
		exclude group:'junit', module: 'junit'
		exclude group: 'org.freehep', module: 'freehep-graphicsio-tests'
	}

	// iText for PDF export (http://www.lowagie.com/iText/)
	compile('com.lowagie:itext:2.1.7'){
		exclude group: 'bouncycastle', module: 'bcmail-jdk14'
		exclude group: 'bouncycastle', module: 'bcprov-jdk14'
		exclude group: 'bouncycastle', module: 'bctsp-jdk14'
	}

	// RSyntaxTextArea adds text fields with syntax highlighting (http://fifesoft.com/rsyntaxtextarea/)
	compile 'com.fifesoft:rsyntaxtextarea:2.6.0'
	compile 'com.fifesoft:autocomplete:2.6.0'

	// JXL for the ability to read, write, and modify old format Microsoft Excel spreadsheets (http://www.jexcelapi.org)
	compile('net.sourceforge.jexcelapi:jxl:2.6.12') { exclude group: 'log4j', module: 'log4j' }

	// Apache POI for  manipulating various file formats based upon Office Open XML standards (http://poi.apache.org/)
	//	compile 'org.apache.poi:poi-ooxml:3.13'
	//	compile 'org.apache.poi:poi-scratchpad:3.13'
	//unfortunately xmlbeans is corrupt (https://issues.apache.org/jira/browse/XMLBEANS-499), so we cannot use the original POI versions as they depend on it.
	compile 'org.apache.servicemix.bundles:org.apache.servicemix.bundles.poi:3.17_1'
//	osgiRuntime('org.apache.santuario:xmlsec:2.0.5') {
//        transitive = false // transitive dependencies not included in OSGi runtime
//    }
//	osgiRuntime('org.apache.servicemix.bundles:org.apache.servicemix.bundles.xmlresolver:1.2_5')

	// JGoodies Looks for TODO (http://www.jgoodies.com/freeware/libraries/looks/)
	compile 'com.jgoodies:looks:2.2.2'

	// JUNG for displaying graphs and trees (http://jung.sourceforge.net/)
	compile 'net.sf.jung:jung-visualization:2.0.1'
	compile 'net.sf.jung:jung-graph-impl:2.0.1'

	// JFreeChart for chart rendering (http://www.jfree.org/jfreechart/)
	compile 'org.jfree:jfreechart:1.0.17'

	// Java Mail Implementation for mail sending (https://javaee.github.io/javamail/)
	compile 'com.sun.mail:javax.mail:1.6.0'

	// Groovy for 'Execute Script' operator (http://groovy.codehaus.org/)
	compile 'org.codehaus.groovy:groovy-all:2.4.10'

	// SwingX for various Swing components (https://swingx.java.net/)
	compile 'org.swinglabs.swingx:swingx-all:1.6.5'

	// XStreams for generic XML serialization (http://xstream.codehaus.org/)
//	compile 'com.thoughtworks.xstream:xstream:1.4.10'
	compile ('org.apache.servicemix.bundles:org.apache.servicemix.bundles.xstream:1.4.10_1') {
//		exclude group: 'xpp3', module: 'xpp3_min'
	}
	//osgiRuntime 'org.apache.servicemix.bundles:org.apache.servicemix.bundles.xpp3:1.1.4c_7'

	// XMLRPC for XMLRPC connections to Bugzilla (http://ws.apache.org/xmlrpc/)
	//compile('org.apache.xmlrpc:xmlrpc-client:3.1.3') { exclude group: 'junit', module: 'junit' }

	// HttpClient used by the Bugzilla XML RPC client (http://hc.apache.org/httpcomponents-client)
	//compile 'org.apache.httpcomponents:httpclient:4.5.1'

	// FileUtils useful for various things
	compile 'commons-io:commons-io:2.6'

	// JAMA for matrix calculations (http://math.nist.gov/javanumerics/jama/)
	compile 'gov.nist.math:jama:1.0.3'

	// commons-math for matrix calculations (http://commons.apache.org/proper/commons-math/)
	compile 'org.apache.commons:commons-math3:3.4.1'

	// commons-lang for different String utility functions (http://commons.apache.org/proper/commons-lang/)
	compile 'commons-lang:commons-lang:2.6'

	// bouncycastle for encryption algorithms (https://www.bouncycastle.org/)
	//compile 'org.bouncycastle:bcprov-jdk15on:1.50'

	// jasypt for simplified encryption (http://www.jasypt.org/)
	//compile 'org.jasypt:jasypt:1.9.1:lite'

	// antlr for parsing expressions (http://www.antlr.org/)
	compile 'org.antlr:antlr4-runtime:4.5.1'
 
	// SLF4J API (http://www.slf4j.org) 
	compile 'org.slf4j:slf4j-api:1.7.13'
	osgiRuntime 'org.slf4j:slf4j-log4j12:1.7.13'

	// add testing suite
	//TODO should be test compile but RapidMiner src/main contains code that references JUnit
	compile 'junit:junit:4.12'

	// JGraphx for automatic operator arrangement (https://github.com/jgraph/jgraphx)
	compile 'com.rapidminer.external:jgraphx:3.9.4'

	// JMathPlot for 2D and 3D plots like Box plot, Stick plot, etc. (https://code.google.com/p/jmathplot/)
	compile 'com.rapidminer.external:jmathplot:1.0.0'
	
	// Microba adds a Swing date picker (http://microba.sf.net/)
	embedded 'com.github.tdbear:microba:0.4.4.3'
	
	// Apache Tika for file MIME type detection (https://tika.apache.org/)
	compile 'org.apache.tika:tika-core:1.18'
	
	// The xalan version used by the JRE for XML handling contains a bug which results in XSLT not working with an enabled SecurityManager
	// Using this version manually to override the JRE fixes the problem. Otherwise all our operator documentation would not work at all 
	compile 'xalan:xalan:2.7.2'
	compile 'xalan:serializer:2.7.2'
	
	// H2 Database used for CTA
	compile 'com.h2database:h2:1.4.194'

	// up until 7.4.1 this was a transitive dependency coming from license-commons (jackson version 2.4.0)
	compile 'com.fasterxml.jackson.core:jackson-core:2.8.9'
	compile 'com.fasterxml.jackson.core:jackson-databind:2.8.9'

	// Advanced Search functionality via Lucene
//	compile 'org.apache.lucene:lucene-queryparser:7.2.0'
	// Highlight Lucene search result matches
//	compile 'org.apache.lucene:lucene-highlighter:7.2.0'
	compile 'org.jsoup:jsoup:1.8.3'

	// access to windows registry
	compile 'net.java.dev.jna:jna-platform:4.5.1'

	// tests require mockito
	testCompile 'org.mockito:mockito-core:2.13.0'

}

task wrapper(type: Wrapper) { gradleVersion = '4.0.1' }

javadoc {
	failOnError = false
}

jar {
	manifest {
	//com.sun.management should be provided.
	//javafx should be provided
//    	instruction 'Import-Package', '!com.sun.*,!sun.*,!com.mxgraph.*,!com.michaelbaranov.microba.*,!com.vlsolutions.swing.*,!com.rapidminer.license.*,!com.rapidminer.gui.osx,!com.apple.*,!com.rapidminer.core.*,!jxl.*,!org.apache.commons.collections15.*,!org.freehep.*,!org.math.plot.*,!com.lowagie.text.*,!edu.uci.ics.jung.*,!Jama,!org.bouncycastle.*,!org.jdesktop.swingx.*,!org.jgraph.*,!org.jfree.*,!com.rapidminer.external.alphanum,!javafx.*,!netscape.*,*'
//    	instruction 'Import-Package', '!com.sun.*,!sun.*,!com.michaelbaranov.microba.*,!com.vlsolutions.swing.*,!com.rapidminer.license.*,!com.rapidminer.gui.osx,!com.apple.*,!com.rapidminer.core.*,!javafx.*,!netscape.*,*'
//    	instruction 'Import-Package', '!com.sun.*,!sun.*,!com.michaelbaranov.microba.*,!com.vlsolutions.swing.*,!com.rapidminer.license.*,!com.rapidminer.gui.osx,!com.apple.*,!com.rapidminer.core.*,!javafx.*,!netscape.*,!org.jsoup.*,org.apache.lucene.analysis;bundle-version="7.2.0",org.apache.lucene.analysis.miscellaneous;bundle-version="7.2.0",org.apache.lucene.analysis.standard;bundle-version="7.2.0",org.apache.lucene.analysis.util;bundle-version="7.2.0",org.apache.lucene.document;bundle-version="7.2.0",org.apache.lucene.index;bundle-version="7.2.0",org.apache.lucene.queryparser.classic;bundle-version="7.2.0",org.apache.lucene.search;bundle-version="7.2.0",org.apache.lucene.search.highlight;bundle-version="7.2.0",org.apache.lucene.store;bundle-version="7.2.0",org.apache.lucene.util;bundle-version="7.2.0",*'
    	instruction 'Import-Package', '!com.sun.*,!sun.*,!com.michaelbaranov.microba.*,!com.vlsolutions.swing.*,!com.rapidminer.license.*,!com.rapidminer.gui.osx,!com.apple.*,!com.rapidminer.core.*,!javafx.*,!netscape.*,!org.jsoup.*,!org.apache.lucene.*,!javax.mail.*,!org.apache.poi.*,!org.antlr.v4.*,*'
//    	instruction '-exportcontents', 'com.vlsolutions.swing.docking,com.vlsolutions.swing.docking.ui,com.vlsolutions.swing.docking.animation,com.vlsolutions.swing.docking.event,com.vlsolutions.swing.docking.ws,com.vlsolutions.swing.toolbars,jxl,jxl.biff,jxl.format,jxl.read.biff,jxl.write,'
    	instruction '-exportcontents', 'com.vlsolutions.swing.docking,com.vlsolutions.swing.docking.ui,com.vlsolutions.swing.docking.animation,com.vlsolutions.swing.docking.event,com.vlsolutions.swing.docking.ws,com.vlsolutions.swing.toolbars'
    	instruction 'Require-Bundle','org.jsoup;bundle-version="[1.8,2)",com.sun.mail.javax.mail;bundle-version="1.6.0",org.antlr.antlr4-runtime-osgi;bundle-version="[4.5.1,5)",org.apache.servicemix.bundles.poi;bundle-version="[3.17,4)",com.sun.jna;bundle-version="[4.5.0,5)",com.sun.jna.platform;bundle-version="[4.5.1,5)"'
    	// In order to allow loading plugins, let the included license bundle use jackson
    	instruction 'DynamicImport-Package', '*'
		//instruction 'Bundle-RequiredExecutionEnvironment', 'JavaSE-1.8'
		//instruction 'Require-Capability', 'osgi.ee;filter:="(&(osgi.ee=JavaSE)(version>=1.7))"'
		instruction '-noee', 'true'
		instruction 'Bundle-ActivationPolicy', 'lazy'
	}
	//From: http://kennethjorgensen.com/blog/2014/fat-jars-with-excluded-dependencies-in-gradle
	dependsOn configurations.runtime
	from {
        (configurations.embedded).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
}

apply from: 'gradle/wsimport.gradle'
apply from: 'gradle/props.gradle'
apply from: 'gradle/tutorial.gradle'
apply from: 'gradle/template.gradle'

// Jacoco for code coverage information
apply from: 'jacoco.gradle'
